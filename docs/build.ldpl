# create docs/index.html
DATA:
template is text
readme is text
html is text

# link parsing
i is number
x is number
len is number
chars is text vector
char is text
link is text
token is text
newfile is text

PROCEDURE:
load file "docs/index.tpl" in template
load file "README.txt" in readme

sub-procedure escape-html
    replace "<" from readme with "&lt;" in readme
    replace ">" from readme with "&gt;" in readme
end sub-procedure
call escape-html

sub-procedure replace-link-in-token
    get index of "https://" from token in x
    if x is equal to 0 then
        in token join "<a href=\"" token "\">" token "</a>"
    end if
end sub-procedure

sub-procedure replace-links
    split readme by "" in chars
    store length of readme in len
    while i is less than len do
        store chars:i in char
        if char is equal to " " then
            in newfile join newfile token char
            store "" in token
        else if char is equal to "\n" then
            call replace-link-in-token
            in newfile join newfile token char
            store "" in token
        else if char is equal to "\r" then
            # ignore
        else
            join token and char in token
        end if
        incr i
    repeat
    join newfile and token in readme
end sub-procedure
call replace-links

replace "{{body}}" from template with readme in html
write html to file "docs/index.html"
