# === LOADER =========================================================
# The loader turns a text file of bytecode into a number vector
# of bytecode.

DATA:
# IN:
loader.input    is text # bytecode in .dinocode text format

# LOCALS:
loader.i        is number
loader.len      is number
loader.char     is text
loader.prev     is text
loader.token    is text
loader.string?  is number

PROCEDURE:
#  IN: loader.input
# OUT: vm.code
#      vm.code.len
#      vm.text
#      vm.text.len
sub-procedure loader.run
    store length of loader.input in loader.len
    store 0 in loader.i
    store 0 in loader.string?
    store "" in loader.token
    while loader.i is less than loader.len do
        store loader.char in loader.prev
        get character at loader.i from loader.input in loader.char

        if loader.string? is equal to 1 then
            join loader.token and loader.char in loader.token
            add 1 and loader.i in loader.i
            if loader.char is equal to "\"" then
                if loader.prev is not equal to "\\" then
                    store 0 in loader.string?
                    store loader.token in vm.text:vm.text.len
                    add 1 and vm.text.len in vm.text.len
                    store "" in loader.token
                end if
            end if
            continue
        end if

        if loader.char is equal to " " then
            if loader.token is not equal to "" then
                store loader.token in vm.code:vm.code.len
                add 1 and vm.code.len in vm.code.len
                store "" in loader.token
            end if
        else if loader.char is equal to "\"" then
            join loader.token and loader.char in loader.token
            store 1 in loader.string?
        else
            join loader.token and loader.char in loader.token
        end if
        add 1 and loader.i in loader.i
    repeat
end sub-procedure